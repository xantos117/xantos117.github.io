<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge">
        <!-- <meta name="viewport" content="width=device-width, initial-scale=1.0"> -->
        <!-- <link rel="stylesheet" href="simple/simple.min.css"> -->
        <link rel="stylesheet" href="basic.css">
        <style>
			.container {
				position: absolute;
				width: 100%;
				height: 100%;
			}

			.deb_slider {
				position: absolute;
				cursor: ew-resize;

				width: 40px;
				height: 40px;
				background-color: #F32196;
				opacity: 0.7;
				border-radius: 50%;

				top: calc(50% - 20px);
				left: calc(0% - 20px);
			}
		</style>
        <title>Watercolors</title>
        <script id="vertex-shader" type="x-shader/x-vertex">
            varying vec2 vUv;
            void main() {
                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
          </script>
          
          <script id="fragment-shader" type="x-shader/x-fragment">
            #include <common>

            varying vec2 vUv;
            uniform vec3 iResolution;
            uniform float iTime;
            uniform vec4 inkColor;
            uniform sampler2D tSource;
            uniform vec2 mousePrior;
            uniform vec2 mousePos;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform bool mDown;
            
            void mainImage( out vec4 fragColor) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 brush = vec4(1.);
                vec4 color1 = inkColor;
                vec4 color2 = vec4(0.0);
                float travelDist = (10. / 1000.) * iTime;

                vec2 diff = (vUv - mousePos)/texel;
                float dist = dot(diff, diff);
                vec4 curColor = texture2D(tSource,vUv);
                vec4 compColor;
                vec2 searchDir;

                compColor = vec4(0);

                for(float i = -1.; i<=1.001;i++){
                    for(float j=-1.;j<=1.001;j++){
                        if(!(i==0. && j==0.)){
                            curColor = texture2D(tSource,vec2(vUv.x + i*texel.x, vUv.y + j*texel.y));
                            compColor.r = max(compColor.r,curColor.r);
                            compColor.g = max(compColor.g,curColor.g);
                            compColor.b = max(compColor.b,curColor.b);
                        }
                    }
                }
                

                if(dist < 50. && mDown) {   
                    fragColor = color1 + curColor;
                }
                else {
                    compColor.r = min(1.,compColor.r);
                    compColor.g = min(1.,compColor.g);
                    compColor.b = min(1.,compColor.b);
                    fragColor = (compColor*0.99);
                }
                
            }
            void downFlow(out vec4 fragColor) {
                fragColor = vec4(0.,0.,1.,0.);
            }
            
            void main() {
                downFlow(gl_FragColor);
            }
          </script>
          <script id="fragment-screen" type="x-shader/x-fragment">
            #include <common>

            varying vec2 vUv;
            uniform vec3 iResolution;
            uniform float iTime;
            uniform vec4 inkColor;
            // (e1,e2,e3,e4)
            uniform sampler2D stVecs;
            // (e5,e6,e7,e8)
            uniform sampler2D diagVecs;
            // (e0, u.x, u.y, rho)
            uniform sampler2D otherVecs;
            uniform sampler2D paperTexture;
            uniform sampler2D reboundTexture;
            uniform sampler2D pigmentTexture1;
            uniform sampler2D pigmentTexture2;
            uniform sampler2D pigmentTexture3;
            uniform sampler2D pigmentTexture4;
            uniform vec2 mousePrior2;
            uniform vec2 mousePrior;
            uniform vec2 mousePos;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform int shaderStage;
            uniform bool mDown;
            uniform int paintTexID;
            uniform float omg;
            uniform float rho0;
            uniform float c;
            uniform float alpha;
            uniform float eta;
            uniform bool ink;
            uniform bool useMixbox;
            uniform float tau;
            uniform float granGam;
            uniform float theta;
            uniform float brushRadius;
            uniform vec4 pigCon;
            uniform vec4 latent;
            uniform vec2 dv;
            uniform vec3 K1;
            uniform vec3 S1;
            uniform vec3 K2;
            uniform vec3 S2;

            // lattice direction vectors
            const vec2 e[9] = vec2[9](vec2(0,0),
                        vec2(1,0),
                        vec2(0,1),
                        vec2(-1,0),
                        vec2(0,-1),
                        vec2(1,1),
                        vec2(-1,1),
                        vec2(-1,-1),
                        vec2(1,-1));
            // lattice geometry constants
            const float w[9] = float[9](4./9.,1./9.,1./9.,1./9.,1./9.,1./36.,1./36.,1./36.,1./36.);

            const float waterEvapRate = 0.0005;
            const float boundEvapRate = 5e-5;
            const float maxWaterCap = 1.;
            const float MAX_KAPPA = 2e20;

            //	<https://www.shadertoy.com/view/Xd23Dh>
            //	by inigo quilez <http://iquilezles.org/www/articles/voronoise/voronoise.htm>
            //

            vec3 hash3( vec2 p ){
                vec3 q = vec3( dot(p,vec2(127.1,311.7)), 
                            dot(p,vec2(269.5,183.3)), 
                            dot(p,vec2(419.2,371.9)) );
                return fract(sin(q)*43758.5453);
            }

            float iqnoise( in vec2 x, float u, float v ){
                vec2 p = floor(x);
                vec2 f = fract(x);
                    
                float k = 1.0+63.0*pow(1.0-v,4.0);
                
                float va = 0.0;
                float wt = 0.0;
                for( int j=-2; j<=2; j++ )
                for( int i=-2; i<=2; i++ )
                {
                    vec2 g = vec2( float(i),float(j) );
                    vec3 o = hash3( p + g )*vec3(u,u,1.0);
                    vec2 r = g - f + o.xy;
                    float d = dot(r,r);
                    float ww = pow( 1.0-smoothstep(0.0,1.414,sqrt(d)), k );
                    va += o.z*ww;
                    wt += ww;
                }
                
                return va/wt;
            }

            //	Classic Perlin 2D Noise 
            //	by Stefan Gustavson
            //
            vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
            vec2 fade(vec2 t) {return t*t*t*(t*(t*6.0-15.0)+10.0);}

            float cnoise(vec2 P){
                vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
                vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
                Pi = mod(Pi, 289.0); // To avoid truncation effects in permutation
                vec4 ix = Pi.xzxz;
                vec4 iy = Pi.yyww;
                vec4 fx = Pf.xzxz;
                vec4 fy = Pf.yyww;
                vec4 i = permute(permute(ix) + iy);
                vec4 gx = 2.0 * fract(i * 0.0243902439) - 1.0; // 1/41 = 0.024...
                vec4 gy = abs(gx) - 0.5;
                vec4 tx = floor(gx + 0.5);
                gx = gx - tx;
                vec2 g00 = vec2(gx.x,gy.x);
                vec2 g10 = vec2(gx.y,gy.y);
                vec2 g01 = vec2(gx.z,gy.z);
                vec2 g11 = vec2(gx.w,gy.w);
                vec4 norm = 1.79284291400159 - 0.85373472095314 * 
                    vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11));
                g00 *= norm.x;
                g01 *= norm.y;
                g10 *= norm.z;
                g11 *= norm.w;
                float n00 = dot(g00, vec2(fx.x, fy.x));
                float n10 = dot(g10, vec2(fx.y, fy.y));
                float n01 = dot(g01, vec2(fx.z, fy.z));
                float n11 = dot(g11, vec2(fx.w, fy.w));
                vec2 fade_xy = fade(Pf.xy);
                vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
                float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
                return 2.3 * n_xy;
            }

            float relax(float f,int i) {
                vec2 u = texture2D(otherVecs,vUv).yz;
                //float alpha = 0.4;
                float rho = texture2D(reboundTexture,vUv).a;
                float kap = texture2D(reboundTexture,vUv).b;
                float deu = dot(e[i],u);
                float psi = smoothstep( 0., alpha, rho);
                float f_eq = w[i] *(rho + rho0 * psi * ((3./(c*c))*deu + (9./(2.*(c*c*c*c))) * deu * deu  - (3./(2.*c*c)) * dot(u,u)));
                return (1. - omg) * f + omg * f_eq;
            }

            float relaxExplicit(float f,int i,vec2 u,float rho) {
                float kap = texture2D(reboundTexture,vUv).b;
                float deu = dot(e[i],u);
                float psi = smoothstep( 0., alpha, rho);
                float f_eq = w[i] *(rho + rho0 * psi * ((3./(c*c))*deu + (9./(2.*(c*c*c*c))) * deu * deu  - (3./(2.*c*c)) * dot(u,u)));
                return (1. - omg) * f + omg * f_eq;
            }

            vec2 getMouseDistance() {
                vec2 texel = vec2(screenWidth, screenHeight);
                //vec2 diff = (vUv - mousePos)*texel;
                //float dist = dot(diff, diff);
                vec2 p1 = mousePrior*texel;
                vec2 p2 = mousePos*texel;
                vec2 p3 = mousePrior2*texel;
                vec2 p0 = vUv*texel;
                vec2 diff = (p2 - p1);
                vec2 diff2 = (p1 - p3);
                vec2 b = p3;
                vec2 a = p1;
                vec2 n = normalize(p2-p1);
                vec2 n2 = normalize(p1 - p3);
                vec2 dvec = (p0-a) - (dot((p0-a),n) * n);
                vec2 dv2 = (p0 - b) - (dot((p0 - b),n2) * n);

                
                
                vec2 v1 = normalize(p2 - p1); // segment 1
                vec2 v2 = normalize(p0 - p1); // p1 to frag
                vec2 v3 = normalize(p1 - p2); // segment 1 reversed
                vec2 v4 = normalize(p0 - p2); // p2 to frag
                vec2 v5 = normalize(p1 - p3); // segment 2
                vec2 v6 = normalize(p0 - p3); // p3 to frag
                vec2 v7 = normalize(p3 - p1); // segment 2 reversed
                
                // dot(v1,v2) > 0. && dot(v3,v4) > 0. = within segment 1
                // dot(v7,v2) > 0. && dot(v5,v6) > 0. = within segment 2
                bool validS1 = length(diff) > 0.001;
                bool validS2 = length(diff2) > 0.001;
                if(!validS1 && !validS2)
                    return abs((p0-p2));

                bool winSeg1 = dot(v1,v2) > 0. && dot(v3,v4) > 0. && validS1;
                bool winSeg2 = dot(v7,v2) > 0. && dot(v5,v6) > 0. && validS2;

                // winSeg1 && !winSeg2 || !winSeg1 && winSeg2 || winSeg1 && winSeg2
                // dvec                 || dv2                || min(dvec,dv2)
                vec2 shortest = mix(dvec,dv2,float(length(dvec)>length(dv2)));
                vec2 closestSeg1 = mix(p0-p1,p0-p2,float(length(p0-p1)>length(p0-p2)));
                vec2 closestPoint = mix(p0-p3,closestSeg1,float(length(p0-p3)>length(closestSeg1)));

                if(winSeg1 || winSeg2){
                    return float(winSeg1 && !winSeg2) * abs(dvec) + float(!winSeg1 && winSeg2) * abs(dv2) + float(winSeg1 && winSeg2) * shortest;
                } else {
                    return closestPoint;
                }
                //return dist;

            }

            void flowPigment(out vec4 fragColor,in sampler2D pigTex, in int pigID) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 stVec = texture2D(stVecs,vUv);
                vec4 diagVec = texture2D(diagVecs,vUv);
                vec4 otherVec = texture2D(otherVecs,vUv);
                vec4 reboundVec = texture2D(reboundTexture,vUv);
                vec4 priorVec = texture2D(pigTex,vUv);

                
                vec2 vel = otherVec.yz;
                float speed = abs(length(vel));
                
                float psurf = priorVec.r;
                float pflow = priorVec.g;
                float pfix = priorVec.b;
                float ptemp = 0.;
                float nu = 0.01;
                float toFix = 0.;
                float ws = reboundVec.r;
                float wf = reboundVec.g;
                float kappa = reboundVec.b;
                float rho = reboundVec.a;
                float rho_p = otherVec.a;

                float dist = length(getMouseDistance());
                if(dist < brushRadius && mDown && (pigID == paintTexID || useMixbox)) {   
                    psurf = float(!useMixbox)*1.0 + float(useMixbox)*pigCon[pigID-1];
                }
                
                if(kappa <= 1.){
                    // wet cell
                    if(rho > 0.){
                        pflow = (pflow * rho + psurf * wf)/(rho + wf);
                        //float dflow = pflow - priorVec.g;
                        psurf = max(psurf - pflow,0.);
                    }
                    
                    //psurf = ptemp;
                    //psurf = max(psurf * reboundVec.r,0.);

                    float wloss = rho_p - rho;
                    float psi = 0.;
                    if(wloss > 0.){
                        psi = (wloss)/rho_p;
                        toFix = pflow*(1. - kappa * granGam) *(max(psi * (1.-smoothstep(0.,1.0,rho)),0.005)) - float(speed > tau*iTime) * (theta * rho * pfix);
                    } else {
                        // out of fluid!
                        toFix = pflow * float(rho == 0.);
                    }
                    //float wloss = max(rho_p - rho,0.);
                    //float fixFactor = mix(0.,wloss/rho_p,float(wloss > 0. && rho_p > 0.));
                    //float mu = 0.005;
                    //fixFactor = max(fixFactor * (1.-smoothstep(0.,mu,rho)),0.005);// - float(speed > tau*iTime) * (theta * rho * pfix);
                    //toFix = mix(mix(fixFactor * pflow,fixFactor * pfix, float(speed > tau*iTime)),pflow,float(rho == 0.));
                    
                    //toFix = min(toFix,pflow);
                    if(pflow > toFix){
                        pflow = mix(pflow - toFix, pflow + toFix, float(speed > tau*iTime));
                        pfix = mix(pfix + toFix, pfix - toFix, float(speed > tau*iTime));
                    }
                    else {
                        pfix = mix(pfix + toFix,pfix - pflow, float(speed > tau*iTime));
                        pflow = mix(0.,toFix,float(speed > tau*iTime));
                    }
                }else {
                    if(wf==0. && rho==0.){
                        pfix += pflow;
                        pflow = 0.;
                    }
                }

                //fragColor = vec4(clamp(psurf,0.,1.),clamp(pflow,0.,1.),clamp(pfix,0.,1.),1.);
                fragColor = vec4(psurf,pflow,pfix,1.);
            }

            void advectPigment(out vec4 fragColor,in sampler2D pigTex, in int pigID){
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 stVec = texture2D(stVecs,vUv);
                vec4 diagVec = texture2D(diagVecs,vUv);
                vec4 otherVec = texture2D(otherVecs,vUv);
                vec4 reboundVec = texture2D(reboundTexture,vUv);
                vec4 priorVec = texture2D(pigTex,vUv);
                
                vec2 vel = otherVec.yz * texel;
                
                float psurf = priorVec.r;
                float pflow = priorVec.g;
                float pflow_p = pflow;
                float pfix = priorVec.b;
                float ptemp = 0.;
                float nu = 0.01;
                float toFix = 0.;
                float ws = reboundVec.r;
                float wf = reboundVec.g;
                float kappa = reboundVec.b;
                float rho_p = otherVec.a;
                float rho = reboundVec.a;
                
                for(int i = 1; i<5;i++) {
                    ptemp += texture2D(stVecs,vUv-e[i])[i] * texture2D(pigTex,vUv-e[i]).g;
                }
                for(int i = 5;i<9;i++) {
                    ptemp += texture2D(diagVecs,vUv-e[i])[i] * texture2D(pigTex,vUv-e[i]).g;
                }
                //ptemp *= (1./rho);

                float t = float(ptemp > eta && wf > 0.);
                if(kappa <= 1.) {
                    // already wet
                    // perhaps velocity should scale this? 
                    if(texture2D(reboundTexture,vUv-vel).b <= 1.){
                        pflow = texture2D(pigTex,vUv - vel).g;
                    }
                }else {
                    pflow = pflow + t * ptemp;
                }

                fragColor = vec4(psurf,pflow,pfix,1.);
            }

            void determineBoundary(out vec4 fragColor) {
                // write out to texture containing values ws, wf, kappa and rho prior
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 stVec = texture2D(stVecs,vUv);
                vec4 diagVec = texture2D(diagVecs,vUv);
                vec4 otherVec = texture2D(otherVecs,vUv);
                vec4 reboundVec = texture2D(reboundTexture,vUv);
                float dist = length(getMouseDistance());
                float ws = reboundVec.r;
                float wf = 0.;
                float kappa = 0.;
                float rho_p = 0.;
                float rho = 0.;
                rho = stVec.r + stVec.g + stVec.b + stVec.a + diagVec.r + diagVec.g + diagVec.b + diagVec.a + otherVec.r;
                rho = max(rho-waterEvapRate, 0.);   
                bool block = false;
                if(dist < brushRadius && mDown) {   
                    ws = 1.0;
                    block == true;
                }
                ws = max(ws - waterEvapRate, 0.);
                wf = clamp(ws,0.,max(maxWaterCap - rho,0.));
                ws = max(ws - wf, 0.);
                
                
                float sumDens = 0.;
                for(int i = 1;i<9;i++) {
                    sumDens = texture2D(reboundTexture,vUv + e[i]*texel).a;
                    if(sumDens > eta) { 
                        block == true;
                    }
                }
                if(rho + wf > eta || block) {
                    vec4 pa = texture2D(paperTexture,vUv);
                    kappa =  (pa.r+pa.g+pa.b)/3.;//iqnoise(100.*vUv, 1., 1.);
                } else {
                    kappa = MAX_KAPPA;
                }

                fragColor = vec4(ws,wf,kappa,rho+wf);
            }

            void streamInPlus(out vec4 fragColor) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 stVec = texture2D(stVecs,vUv);
                vec4 rebound = texture2D(reboundTexture,vUv);

                float dist = length(getMouseDistance());
                vec2 ddv = vec2(0.);

                vec4 tex1 = texture2D(stVecs,vUv + e[1]*texel);
                vec4 tex2 = texture2D(stVecs,vUv + e[2]*texel);
                vec4 tex3 = texture2D(stVecs,vUv + e[3]*texel);
                vec4 tex4 = texture2D(stVecs,vUv + e[4]*texel);
                vec4 reb1 = texture2D(reboundTexture,vUv - e[1]*texel);
                vec4 reb2 = texture2D(reboundTexture,vUv - e[2]*texel);
                vec4 reb3 = texture2D(reboundTexture,vUv - e[3]*texel);
                vec4 reb4 = texture2D(reboundTexture,vUv - e[4]*texel);
                float kap1 = min((rebound.b + reb3.b)/2.,1.0);
                float kap2 = min((rebound.b + reb4.b)/2.,1.0);
                float kap3 = min((rebound.b + reb1.b)/2.,1.0);
                float kap4 = min((rebound.b + reb2.b)/2.,1.0);

                stVec.r = float(rebound.b <= 1.) * ((kap1) * stVec.r + (1.-kap1) * tex3.r) + float(rebound.b > 1.) * (tex1.b);
                //stVec.r = (kap1) * stVec.r + (1.-kap1) * tex3.b;
                stVec.r = float(rebound.b <= 1.) * max(stVec.r - float(kap1>=1.) * boundEvapRate,0.) + float(rebound.b > 1.) * stVec.r;

                stVec.g = float(rebound.b <= 1.) * ((kap2) * stVec.g + (1.-kap2) * tex4.g) + float(rebound.b > 1.) * (tex2.a);
                //stVec.g = (kap2) * stVec.g + (1.-kap2) * tex4.a;
                stVec.g = float(rebound.b <= 1.) * max(stVec.g - float(kap2>=1.) * boundEvapRate,0.) + float(rebound.b > 1.) * stVec.g;

                stVec.b = float(rebound.b <= 1.) * ((kap3) * stVec.b + (1.-kap3) * tex1.b) + float(rebound.b > 1.) * (tex3.r);
                //stVec.b = (kap3) * stVec.b + (1.-kap3) * tex1.r;
                stVec.b = float(rebound.b <= 1.) * max(stVec.b - float(kap3>=1.) * boundEvapRate,0.) + float(rebound.b > 1.) * stVec.b;

                stVec.a = float(rebound.b <= 1.) * ((kap4) * stVec.a + (1.-kap4) * tex2.a) + float(rebound.b > 1.) * (tex4.g);
                //stVec.a = (kap4) * stVec.a + (1.-kap4) * tex2.g;
                stVec.a = float(rebound.b <= 1.) * max(stVec.a - float(kap4>=1.) * boundEvapRate,0.) + float(rebound.b > 1.) * stVec.a;

                fragColor = stVec;
            }

            void streamInX(out vec4 fragColor) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 diagVec = texture2D(diagVecs,vUv);
                vec4 rebound = texture2D(reboundTexture,vUv);

                float dist = length(getMouseDistance());
                vec2 ddv = vec2(0.);

                vec4 tex1 = texture2D(diagVecs,vUv - e[5]*texel);
                vec4 tex2 = texture2D(diagVecs,vUv - e[6]*texel);
                vec4 tex3 = texture2D(diagVecs,vUv - e[7]*texel);
                vec4 tex4 = texture2D(diagVecs,vUv - e[8]*texel);
                vec4 reb1 = texture2D(reboundTexture,vUv - e[5]*texel);
                vec4 reb2 = texture2D(reboundTexture,vUv - e[6]*texel);
                vec4 reb3 = texture2D(reboundTexture,vUv - e[7]*texel);
                vec4 reb4 = texture2D(reboundTexture,vUv - e[8]*texel);
                float kap1 = min((rebound.b + reb3.b)/2.,1.0);
                float kap2 = min((rebound.b + reb4.b)/2.,1.0);
                float kap3 = min((rebound.b + reb1.b)/2.,1.0);
                float kap4 = min((rebound.b + reb2.b)/2.,1.0);

                diagVec.r = float(rebound.b <= 1.) * ((kap1) * diagVec.r + (1.-kap1) * tex3.r) + float(rebound.b > 1.) * (tex1.b);
                //diagVec.r = (kap1) * diagVec.r + (1.-kap1) * tex3.b;
                diagVec.r = float(rebound.b <= 1.) * max(diagVec.r - float(kap1==1.) * boundEvapRate,0.) + float(rebound.b > 1.) * diagVec.r;

                diagVec.g = float(rebound.b <= 1.) * ((kap2) * diagVec.g + (1.-kap2) * tex4.g) + float(rebound.b > 1.) * (tex2.a);
                //diagVec.g = (kap2) * diagVec.g + (1.-kap2) * tex4.a;
                diagVec.g = float(rebound.b <= 1.) * max(diagVec.g - float(kap2==1.) * boundEvapRate,0.) + float(rebound.b > 1.) * diagVec.g;

                diagVec.b = float(rebound.b <= 1.) * ((kap3) * diagVec.r + (1.-kap3) * tex1.b) + float(rebound.b > 1.) * (tex3.r);
                //diagVec.b = (kap3) * diagVec.b + (1.-kap3) * tex1.r;
                diagVec.b = float(rebound.b <= 1.) * max(diagVec.b - float(kap3==1.) * boundEvapRate,0.) + float(rebound.b > 1.) * diagVec.b;

                diagVec.a = float(rebound.b <= 1.) * ((kap4) * diagVec.r + (1.-kap4) * tex2.a) + float(rebound.b > 1.) * (tex4.g);
                //diagVec.a = (kap4) * diagVec.a + (1.-kap4) * tex2.g;
                diagVec.a = float(rebound.b <= 1.) * max(diagVec.a - float(kap4==1.) * boundEvapRate,0.) + float(rebound.b > 1.) * diagVec.a;

                fragColor = diagVec;
            }

            void updateDensVel(out vec4 fragColor) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 stVec = texture2D(stVecs,vUv);
                vec4 diagVec = texture2D(diagVecs,vUv);
                vec4 otherVec = texture2D(otherVecs,vUv);
                vec4 reboundVec = texture2D(reboundTexture,vUv);

                float dist = length(getMouseDistance());

                float rho = reboundVec.a;
                //if(dist < brushRadius && mDown) {   
                //    rho = 1.0;
                //    otherVec.r = 1.0;
                //}
                //else {
                //    //rho *= 0.995;
                //}
                vec2 ddv = vec2(0.);

                //rho = stVec.r + stVec.g + stVec.b + stVec.a + diagVec.r + diagVec.g + diagVec.b + diagVec.a + otherVec.r;
                // should this carry the previous velocity? - if there's fluid, that velocity should be applied there
                vec2 u = e[1] * stVec.r + e[2] * stVec.g + e[3] * stVec.b + e[4] * stVec.a + e[5] * diagVec.r + e[6] * diagVec.g + e[7] * diagVec.b + e[8] * diagVec.a; 
                u *= (1./rho0);
                if(dist < brushRadius && mDown) {  
                    ddv = (mousePos - mousePrior)/iTime;
                    float topspeed = length(dv/2.);
                    float curspeed = length(ddv);
                    if(curspeed > 0.){
                        ddv = normalize(ddv) * ((curspeed/topspeed) * 0.05 + 0.01);
                        u = u + ddv;
                    }
                }

                fragColor = vec4(relaxExplicit(otherVec.r,0,u,rho), u.x, u.y, rho);
            }

            void relaxPlus(out vec4 fragColor) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 stVec = texture2D(stVecs,vUv);

                stVec.r = relax(stVec.r,1);
                stVec.g = relax(stVec.g,2);
                stVec.b = relax(stVec.b,3);
                stVec.a = relax(stVec.a,4);

                fragColor = stVec;
            }

            void relaxX(out vec4 fragColor) {
                vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
                vec4 diagVec = texture2D(diagVecs,vUv);

                diagVec.r = relax(diagVec.r,5);
                diagVec.g = relax(diagVec.g,6);
                diagVec.b = relax(diagVec.b,7);
                diagVec.a = relax(diagVec.a,8);

                fragColor = diagVec;
            }

            void drawToScreen(out vec4 fragColor) {
                vec4 otherVec = texture2D(otherVecs,vUv);
                vec4 paperVec = texture2D(paperTexture,vUv);
                vec4 boundVec = texture2D(reboundTexture,vUv);
                vec4 redVec = texture2D(pigmentTexture1,vUv);
                float sumRed = redVec.r + redVec.g + redVec.b;
                float intens = clamp(sumRed/3.,0.,1.);
                fragColor = vec4(intens,0.,0.,intens);
                //if(sumRed<0.01) {
                //    fragColor = vec4(0.8,0.8,0.8,1.);
                //}else {
                //    fragColor = vec4(intens,0.,0.,1.);
                //    //fragColor = redVec;
                //}
                //vec4 lColor = mix(paperVec+vec4(1.),vec4(vec3(0.),1.),intens);
                //fragColor = colormap(boundVec.b);
            }

            void drawUv(out vec4 fragColor){
                vec4 height = vec4(iqnoise(100.*vUv, 1., 1.));
                //fragColor = height;//vec4(height,height,height,1.0);u
                //fragColor = vec4(texture2D(otherVecs,vUv).yz,0.0,1.0);
                //vec4 reboundVec = texture2D(reboundTexture,vUv);
                //vec4 otherVec = texture2D(otherVecs,vUv);
                //float sumVec = stVec.r + stVec.g + stVec.b + stVec.a;
                //float bVal = reboundVec.a;
                vec4 color1 = vec4(1.0,0.1,0.1,1.0);
                float sumVec = (height.r + height.g + height.b)/3.;
                vec4 outColor = mix(color1, height, float(sumVec > 0.5));
                fragColor = texture2D(paperTexture,vUv);
            }

            vec3 coefs[20] = vec3[20](
                vec3(1.0*+0.07717053,1.0*+0.02826978,1.0*+0.24832992),
                vec3(1.0*+0.95912302,1.0*+0.80256528,1.0*+0.03561839),
                vec3(1.0*+0.74683774,1.0*+0.04868586,1.0*+0.00000000),
                vec3(1.0*+0.99518138,1.0*+0.99978149,1.0*+0.99704802),
                vec3(3.0*+0.01606382,3.0*+0.27787927,3.0*+0.10838459),
                vec3(3.0*-0.22715650,3.0*+0.48702601,3.0*+0.35660312),
                vec3(3.0*+0.09019473,3.0*-0.05108290,3.0*+0.66245019),
                vec3(3.0*+0.26826063,3.0*+0.22364570,3.0*+0.06141500),
                vec3(3.0*-0.11677001,3.0*+0.45951942,3.0*+1.22955000),
                vec3(3.0*+0.35042682,3.0*+0.65938413,3.0*+0.94329691),
                vec3(3.0*+1.07202375,3.0*+0.27090076,3.0*+0.34461513),
                vec3(3.0*+0.92964458,3.0*+0.13855183,3.0*-0.01495765),
                vec3(3.0*+1.00720859,3.0*+0.85124701,3.0*+0.10922038),
                vec3(3.0*+0.98374897,3.0*+0.93733704,3.0*+0.39192814),
                vec3(3.0*+0.94225681,3.0*+0.26644346,3.0*+0.60571754),
                vec3(3.0*+0.99897033,3.0*+0.40864351,3.0*+0.60217887),
                vec3(6.0*+0.31232351,6.0*+0.34171197,6.0*-0.04972666),
                vec3(6.0*+0.42768261,6.0*+1.17238033,6.0*+0.10429229),
                vec3(6.0*+0.68054914,6.0*-0.23401393,6.0*+0.35832587),
                vec3(6.0*+1.00013113,6.0*+0.42592007,6.0*+0.31789917)
            );

            void mixbox(vec4 c,out vec3 rgb)
            { 
                float c00 = c[0]*c[0];
                float c11 = c[1]*c[1];
                float c22 = c[2]*c[2];
                float c33 = c[3]*c[3];
                float c01 = c[0]*c[1];
                float c02 = c[0]*c[2];

                float weights[20] = 
                            float[20](
                                c[0]*c00,
                                c[1]*c11,
                                c[2]*c22,
                                c[3]*c33,
                                c00*c[1],
                                c01*c[1],
                                c00*c[2],
                                c02*c[2],
                                c00*c[3],
                                c[0]*c33,
                                c11*c[2],
                                c[1]*c22,
                                c11*c[3],
                                c[1]*c33,
                                c22*c[3],
                                c[2]*c33,
                                c01*c[2],
                                c01*c[3],
                                c02*c[3],
                                c[1]*c[2]*c[3]
                            );

                for(int i=0;i<3;i++) { rgb[i] = 0.0; }  

                for(int j=0;j<20;j++)
                    for(int i=0;i<3;i++)
                    {
                        rgb[i] += weights[j]*coefs[j][i];
                    }
            }

            void getMixColor(out vec4 fragColor) {
                vec4 thick1 = texture2D(pigmentTexture1,vUv);
                vec4 thick2 = texture2D(pigmentTexture2,vUv);
                vec4 thick3 = texture2D(pigmentTexture3,vUv);
                vec4 thick4 = texture2D(pigmentTexture4,vUv);
                vec4 reboundVec = texture2D(reboundTexture,vUv);

                float c1 = (thick1.r + thick1.g + thick1.b);
                float c2 = (thick2.r + thick2.g + thick2.b);
                float c3 = (thick3.r + thick3.g + thick3.b);
                float c4 = (thick4.r + thick4.g + thick4.b);
                float pig_amt = min(c1+c2+c3+c4,1.0);
                vec4 c = vec4(c1,c2,c3,c4);
                vec3 rgb = vec3(0.);
                mixbox(c,rgb);
                float r = clamp(rgb[0]+latent[0],0.0f,1.0f);
                float g = clamp(rgb[1]+latent[1],0.0f,1.0f);
                float b = clamp(rgb[2]+latent[2],0.0f,1.0f);
                vec4 fColor = vec4(rgb,pig_amt);
                vec4 pColor = texture2D(paperTexture,vUv);
                //fragColor = mix(pColor,fColor,pig_amt);
                fragColor = fColor;
            }

            vec3 reflKM(in vec3 R1, in vec3 R2, in vec3 T1, in vec3 T2){
                return R1 + (T1*T1*R2)/(1.-(R1*R2));
            }
            vec3 transKM(in vec3 R1, in vec3 R2, in vec3 T1, in vec3 T2){
                return T1*T2/(1.-R1*R2);
            }

            void getKMColor(out vec4 fragColor){
                // assuming given K and S
                vec4 thick1 = texture2D(pigmentTexture1,vUv);
                vec4 thick2 = texture2D(pigmentTexture2,vUv);
                vec4 thick = thick1+thick2;
                float d = clamp((thick.r + thick.g + thick.b)/3.,0.,1.0);
                vec3[3] kmix = vec3[3](K1 * thick1.r + K2 * thick2.r,
                                 K1 * thick1.g + K2 * thick2.g,
                                 K1 * thick1.b + K2 * thick2.b);
                vec3[3] smix = vec3[3](S1 * thick1.r + S2 * thick2.r,
                                 S1 * thick1.g + S2 * thick2.g,
                                 S1 * thick1.b + S2 * thick2.b);
                //vec3 R = 1. + kmix[2]/smix[2] - sqrt((kmix[2]/smix[2]) * (kmix[2]/smix[2]) + 2. * (kmix[2]/smix[2]));
                vec3 T = vec3(0.);
                vec3 R = vec3(0.);
                //R = texture2D(paperTexture,vUv).rgb;
                vec3 a, b, bsd, c;
                a = b = bsd = c = vec3(0.);
                //if(kmix[2].x+kmix[2].y+kmix[2].z > 0. && smix[2].x+smix[2].y+smix[2].z > 0.){
                //    a = 1. + (kmix[2]/smix[2]);
                //    b = sqrt(a*a - 1.);
                //    bsd = b*smix[2]*thick[2];
                //    c = a * sinh(bsd) + b * cosh(bsd);
                //    R = 1./(a + (b * tanh(1./bsd)));
                //    T = b/c;
                //}

                for(int i = 1;i<3;i++){
                    if(kmix[i].x+kmix[i].y+kmix[i].z > 0. && smix[i].x+smix[i].y+smix[i].z > 0.){
                        a = 1. + (kmix[i]/smix[i]);
                        b = sqrt(a*a - 1.);
                        bsd = b*smix[i]*thick[i];
                        c = a * sinh(bsd) + b * cosh(bsd);
                        vec3 rTemp = 1./(a + b * tanh(1./bsd));
                        vec3 tTemp = b/c;
                        R = reflKM(rTemp,R,tTemp,T);
                        T = transKM(rTemp,R,tTemp,T);
                    }
                }

                vec3 paper = T * texture2D(paperTexture,vUv).rgb;
                //vec3 R = 1. + (K/S) - sqrt((K/S) * (K/S) + 2. * (K/S));
                fragColor = vec4(R+paper,d);
            }

            void main() {
                //vec4 rPig = texture2D(pigmentTexture1,vUv);
                switch (shaderStage) {
                    case 0:
                        streamInPlus(gl_FragColor);
                        break;
                    case 1:
                        streamInX(gl_FragColor);
                        break;
                    case 2:
                        updateDensVel(gl_FragColor);
                        break;
                    case 3:
                        relaxPlus(gl_FragColor);
                        break;
                    case 4: 
                        relaxX(gl_FragColor);
                        break;
                    case 5:
                        determineBoundary(gl_FragColor);
                        break;
                    case 6:
                        flowPigment(gl_FragColor,pigmentTexture1,1);
                        break;
                    case 7:
                        advectPigment(gl_FragColor,pigmentTexture1,1);
                        break;
                    case 8:
                        flowPigment(gl_FragColor,pigmentTexture2,2);
                        break;
                    case 9:
                        advectPigment(gl_FragColor,pigmentTexture2,2);
                        break;
                    case 10:
                        flowPigment(gl_FragColor,pigmentTexture3,3);
                        break;
                    case 11:
                        advectPigment(gl_FragColor,pigmentTexture3,3);
                        break;
                    case 12:
                        flowPigment(gl_FragColor,pigmentTexture4,4);
                        break;
                    case 13:
                        advectPigment(gl_FragColor,pigmentTexture4,4);
                        break;
                    case 14:
                        drawUv(gl_FragColor);
                        break;
                    default:
                        //drawToScreen(gl_FragColor);
                        if(useMixbox){
                            getMixColor(gl_FragColor);
                        }else{
                            getKMColor(gl_FragColor);
                        }
                        break;
                }
            }
          </script>
          
    </head>

    <body>    
        <div class="container">
			<div class="deb_slider"></div>
		</div>
        <script>
            var Module = {
                onRuntimeInitialized: function() {
                var retVector = Module.mixbox_vec_wrapper(10, 20, 230);
                for (var i = 0; i < retVector.size(); i++) {
                    console.log("Vector Value: ", retVector.get(i));
                }
                }
            };
        </script>
        <script type="module" src="quick_example.js"></script>
        <script type="module" src="bundle.js"></script>
    </body>
</html>